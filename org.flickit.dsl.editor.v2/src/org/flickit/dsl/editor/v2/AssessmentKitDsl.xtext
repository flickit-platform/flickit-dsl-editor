grammar org.flickit.dsl.editor.v2.AssessmentKitDsl with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate assessmentKitDsl "http://www.flickit.org/dsl/editor/v2/AssessmentKitDsl"

Root:
	includes+=Include*
    (elements+=BaseInfo)*;

Include:
	'import' importURI=STRING;
 
BaseInfo:
    AssessmentKit | Subject | Questionnaire | Attribute | Question | AnswerRange | Level;

AssessmentKit:
    'kit' name=ID '{'
    ('title:' title=STRING)
    levels+=Level*
    '}';

Level:
    'level' name=ID '{'
    ('title:' title=STRING)
    ('description:' description=STRING)
    ('value:' value=INT)
    (competence+=Competence (',' competence+=Competence)*)?
    '}';

Competence:
    'competence:' '[' 
    competenceValues+=CompetenceValue (',' competenceValues+=CompetenceValue)* 
    ']';

CompetenceValue:
    level=[Level] ':' value=INT '%';

Subject:
    'subject' name=ID '{'
    ('title:' title=STRING)
    ('description:' description=STRING)
    ('index:' index=INT)?
    ('weight:' weight=INT)?
    '}';

Questionnaire:
    'questionnaire' name=ID '{'
    ('title:' title=STRING)
    ('description:' description=STRING)
    ('index:' index=INT)?
    '}';

Attribute:
    'attribute' name=ID '{'
    ('title:' title=STRING)
    ('description:' description=STRING)
    ('index:' index=INT)?
    ('subject:' subject=[Subject])
    ('weight:' weight=INT)?
    '}';

Question:
    'question' name=ID '{'
    'questionnaire:' questionnaire=[Questionnaire]
    ('hint:' hint=STRING)?
    'title:' title=STRING
    ('options:' options+=STRING (',' options+=STRING)* 'with' 'values' '[' values+=Value (',' values+=Value)* ']' | 'answerRange:' answerRange=[AnswerRange])
    ('cost:' cost=Integer)?
    ('advisable:' advisable=Boolean)?
    (affectsAttribute+=AffectsAttribute)+
    ('mayNotBeApplicable:' mayNotBeApplicable=Boolean)?
     '}';

AnswerRange:
  'answerRange' name=ID '{'
  'title:' title=STRING
  'options:' options+=STRING (',' options+=STRING)* 'with' 'values' '[' values+=Value (',' values+=Value)* ']'
  '}'
;     

AffectsAttribute :
	'affects' attribute=[Attribute]
		onLevel+=OnLevel
		(onLevel+=OnLevel)*;

OnLevel :
	'on' 'level' level=[Level] ('with' 'weight' weight=INT)?;

Integer:
	INT;

FLOAT:
	INT '.' INT;

Value:
	INT | FLOAT;

Boolean:
    'true' | 'false';
